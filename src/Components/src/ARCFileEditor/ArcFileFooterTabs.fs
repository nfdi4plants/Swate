namespace Swate.Components.ArcFileEditor

open Feliz
open Fable.Core
open ARCtrl
open Swate.Components.ArcFileEditor.Types
open Swate.Components.Shared
open Swate.Components
open Swate.Components.JsBindings
open Fable.Core.JsInterop

module private ArcFileFooterTabsHelper =

    [<Literal>]
    let FooterTabIdDataKey = "footertabid"

    [<Literal>]
    let TableDragIdPrefix = "table-"

    let mkTableDragId (index: int) = $"{TableDragIdPrefix}{index}"

    let tryParseTableDragId (i: string) =
        if i.StartsWith TableDragIdPrefix then
            let indexStr = i.Substring(TableDragIdPrefix.Length)

            match System.Int32.TryParse indexStr with
            | true, index -> Some index
            | _ -> None
        else
            None

    let resolveDropTargetTableIndex (targetId: string) = tryParseTableDragId targetId

    let tryGetDndEventId (eventNode: obj) =
        if isNull eventNode then
            None
        else
            let idObj: obj = eventNode?``id``

            if isNull idObj then None else Some(string idObj)

    let dndObjectProps (obj: Swate.Components.JsBindings.IObject) = [
        for key in Swate.Components.JsBindings.Object.keys obj do
            prop.custom (key, obj.get key)
    ]

    type prop with
        static member dataFooterTabId(arctiveView: ActiveView) : IReactProperty =
            match arctiveView with
            | ActiveView.DataMap -> "datamap"
            | ActiveView.Metadata -> "metadata"
            | ActiveView.Table index -> mkTableDragId index
            |> fun v -> unbox ($"data-{FooterTabIdDataKey}", v)

    let tryParseDataFooterTabId (i: string) =
        match i with
        | "metadata" -> Some ActiveView.Metadata
        | "datamap" -> Some ActiveView.DataMap
        | _ ->
            match tryParseTableDragId i with
            | Some index -> Some(ActiveView.Table index)
            | None -> None

    module ContextMenu =

        let onSpawn (elementRef: IRefValue<option<Browser.Types.HTMLElement>>) =
            (fun (e: Browser.Types.MouseEvent) ->
                let target = e.target :?> Browser.Types.HTMLElement

                match target.closest ($"[data-{FooterTabIdDataKey}]"), elementRef.current with
                | Some cell, Some container when container.contains (cell) ->
                    let cell = cell :?> Browser.Types.HTMLElement
                    let tabId: string = cell?dataset?footertabid
                    unbox (tryParseDataFooterTabId tabId)
                | _ -> None
            )

open ArcFileFooterTabsHelper

type private TableTabViewModel = {
    index: int
    tableName: string
    isEditorMode: bool
    isActive: bool
}

[<Erase; Mangle(false)>]
type ArcFileFooterTabs =

    static member private BaseTab
        (
            children: ReactElement,
            onClick: Browser.Types.MouseEvent -> unit,
            ?iconClass: string,
            ?isActive: bool,
            ?key,
            ?extraProps: IReactProperty list,
            // This arg is used to get tab information for context menu
            ?activeView: ActiveView
        ) =
        Html.button [
            if extraProps.IsSome then
                yield! extraProps.Value
            if activeView.IsSome then
                prop.dataFooterTabId activeView.Value
            prop.className [
                "swt:tab swt:items-center swt:min-w-fit"
                match isActive with
                | Some true -> "swt:tab-active"
                | _ -> ()
            ]
            prop.onClick onClick
            prop.children [
                if iconClass.IsSome then
                    Html.i [ prop.className [ iconClass.Value; "swt:min-w-4" ] ]
                children
            ]
        ]

    [<ReactMemoComponent(AreEqualFn.FsEqualsButFunctions)>]
    static member private TableTab(index: int, tableName: string, onClick, isActive: bool, ?onDoubleClick, ?key: int) =

        let sortable = DndKit.useSortable ({| id = mkTableDragId index |})

        let style = [
            style.custom ("transform", DndKit.CSS.Transform.toString sortable.transform)
            style.custom ("transition", sortable.transition)
            style.cursor.grab
        ]

        let dndProps = [
            prop.id (mkTableDragId index)
            prop.ref sortable.setNodeRef
            prop.style style
        ]

        let dndProps =
            dndProps
            @ dndObjectProps sortable.attributes
            @ dndObjectProps sortable.listeners
            @ [
                prop.onDoubleClick (fun e ->
                    match onDoubleClick with
                    | Some handler -> handler e
                    | None -> ()
                )
            ]

        ArcFileFooterTabs.BaseTab(
            Html.span [ prop.text tableName ],
            onClick,
            "swt:iconify swt:fluent--table-24-regular",
            isActive = isActive,
            activeView = ActiveView.Table index,
            extraProps = dndProps,
            ?key = key
        )

    [<ReactMemoComponent(AreEqualFn.FsEqualsButFunctions)>]
    static member private EditTableNameTab
        (currentName: string, onNameChange: string -> unit, closeEdit: unit -> unit, isActive: bool)
        =
        let localInput, setLocalInput = React.useState currentName

        let submit =
            fun (newName: string) ->
                let trimmed = newName.Trim()

                match System.String.IsNullOrWhiteSpace trimmed || trimmed = currentName with
                | true -> ()
                | _ -> onNameChange trimmed

        ArcFileFooterTabs.BaseTab(
            Html.input [
                prop.autoFocus true
                prop.className "swt:input swt:input-ghost swt:input-xs swt:w-full swt:max-w-xs"
                prop.value localInput
                prop.onChange (fun (e: string) -> setLocalInput e)
                prop.onKeyDown (fun (e: Browser.Types.KeyboardEvent) ->
                    match e.code with
                    | kbdEventCode.enter ->
                        e.preventDefault ()
                        submit localInput
                    | kbdEventCode.escape ->
                        e.preventDefault ()
                        setLocalInput currentName
                        closeEdit ()
                    | _ -> ()
                )
                prop.onBlur (fun _ -> closeEdit ())
            ],
            ignore,
            isActive = isActive
        )

    [<ReactMemoComponent(AreEqualFn.FsEqualsButFunctions)>]
    static member private MetadataTab(label: string, onClick, isActive: bool) =
        ArcFileFooterTabs.BaseTab(
            Html.span label,
            onClick,
            "swt:iconify swt:fluent--info-20-filled",
            isActive = isActive,
            activeView = ActiveView.Metadata,
            key = "MetadataTab"
        )

    [<ReactMemoComponent(AreEqualFn.FsEqualsButFunctions)>]
    static member private DataMapTab(onClick, isActive: bool) =

        ArcFileFooterTabs.BaseTab(
            Html.span "DataMap",
            onClick,
            "swt:iconify swt:fluent--map-16-regular",
            isActive = isActive,
            activeView = ActiveView.DataMap,
            key = "DataMapTab"
        )

    [<ReactMemoComponent(AreEqualFn.FsEqualsButFunctions)>]
    static member private PlusBtn(onClick) =

        ArcFileFooterTabs.BaseTab(Html.none, onClick, "swt:iconify swt:fluent--add-12-filled")

    [<ReactComponent>]
    static member private ContextMenu
        (
            elementRef: IRefValue<option<Browser.Types.HTMLElement>>,
            setEditorMode: int option -> unit,
            deleteTable: int -> unit
        ) =


        let delete (tableIndex: int) = fun _ -> deleteTable tableIndex

        let rename (tableIndex: int) =
            fun _ -> setEditorMode (Some tableIndex)

        let children =
            fun e ->
                let activeView = unbox<ActiveView option> e

                match activeView with
                | Some(ActiveView.Table index) -> [
                    Swate.Components.ContextMenuItem(
                        Html.span "Rename Table",
                        icon =
                            Html.i [
                                prop.className "swt:iconify swt:fluent--slide-text-title-edit-20-regular swt:size-4"
                            ],
                        onClick = rename index
                    )
                    Swate.Components.ContextMenuItem(
                        Html.span "Delete Table",
                        icon =
                            Html.i [
                                prop.className "swt:iconify swt:fluent--delete-20-filled swt:size-4"
                            ],
                        onClick = delete index
                    )
                  ]
                | _ -> []


        Swate.Components.ContextMenu.ContextMenu(children, ref = elementRef, onSpawn = ContextMenu.onSpawn elementRef)

    [<ReactComponent>]
    static member DragAndDropContainer(tableIds: ResizeArray<string>, handleDragEnd, children: ReactElement) =

        let pointerSensor =
            DndKit.useSensor (
                DndKit.PointerSensor,
                {|
                    activationConstraint = {| distance = 8 |}
                |}
            )

        let sensors = DndKit.useSensors [| pointerSensor |]

        DndKit.DndContext(
            sensors = sensors,
            onDragEnd = handleDragEnd,
            collisionDetection = DndKit.closestCenter,
            children =
                DndKit.SortableContext(
                    items = tableIds,
                    strategy = DndKit.horizontalListSortingStrategy,
                    children = children
                )
        )

    [<ReactComponent>]
    static member Main
        (arcFile: ArcFiles, activeView: ActiveView, setActiveView: ActiveView -> unit, setArcFile: ArcFiles -> unit)
        =
        let tables = arcFile.ArcTables()
        let canAddTable = arcFile.CanCreateTables()
        let canRenderDataMap = arcFile.CanRenderDataMapView()
        let tabsRef = React.useElementRef ()

        let isEditorModeTableTab, setIsEditorModeTableTab =
            React.useState (None: int option)

        let arcFileHash = arcFile.GetHashCode()

        let metadataTabLabel =
            React.useMemo (
                (fun () ->
                    match arcFile with
                    | ArcFiles.Assay _ -> "Assay"
                    | ArcFiles.Study _ -> "Study"
                    | ArcFiles.Investigation _ -> "Investigation"
                    | ArcFiles.Run _ -> "Run"
                    | ArcFiles.Workflow _ -> "Workflow"
                    | ArcFiles.Template _ -> "Template"
                    | ArcFiles.DataMap _ -> "Datamap"
                ),
                [| box arcFileHash |]
            )

        let setEditorMode =
            React.useCallback (
                (fun (nextMode: int option) -> setIsEditorModeTableTab nextMode),
                [| box setIsEditorModeTableTab |]
            )

        let closeEditorMode =
            React.useCallback ((fun () -> setIsEditorModeTableTab None), [| box setIsEditorModeTableTab |])

        let activateMetadataView =
            React.useCallback ((fun _ -> setActiveView ActiveView.Metadata), [| box setActiveView |])

        let activateDataMapView =
            React.useCallback ((fun _ -> setActiveView ActiveView.DataMap), [| box setActiveView |])

        let activateTableView =
            React.useCallback (
                (fun (tableIndex: int) -> setActiveView (ActiveView.Table tableIndex)),
                [| box setActiveView |]
            )

        let openTableNameEditor =
            React.useCallback (
                (fun (tableIndex: int) -> setIsEditorModeTableTab (Some tableIndex)),
                [| box setIsEditorModeTableTab |]
            )

        let tableNamesKey =
            tables |> Seq.map (fun table -> table.Name) |> String.concat "||"

        let addNewTable =
            fun _ ->
                closeEditorMode ()

                if canAddTable then
                    let nextName = Helper.createNewTableName tables.Tables
                    let nextTable = ArcTable.init nextName

                    arcFile.ArcTables().AddTable nextTable

                    setArcFile (ArcFiles.refreshRef arcFile)
                    setActiveView (ActiveView.Table(tables.TableCount - 1))

        let deleteTable (tableIndex: int) =
            closeEditorMode ()
            arcFile.ArcTables().RemoveTableAt tableIndex

            match activeView with
            | ActiveView.Table i when i = tableIndex -> setActiveView ActiveView.Metadata
            | ActiveView.Table i when i > tableIndex -> setActiveView (ActiveView.Table(i - 1))
            | _ -> ()

            setArcFile (ArcFiles.refreshRef arcFile)

        let updateTableOrder (oldIndex: int, newIndex: int) =
            closeEditorMode ()
            arcFile.ArcTables().MoveTable(oldIndex, newIndex)
            let lastIndex = tables.TableCount - 1
            let nextActiveIndex = max 0 (min newIndex lastIndex)
            setActiveView (ActiveView.Table nextActiveIndex)
            setArcFile (ArcFiles.refreshRef arcFile)

        let handleDragEnd =
            React.useCallback (
                (fun (event: DndKit.IDndKitEvent) ->

                    if isEditorModeTableTab.IsSome then
                        ()
                    else
                        match tryGetDndEventId (box event.active), tryGetDndEventId (box event.over) with
                        | Some activeId, Some overId when activeId <> overId ->
                            match tryParseTableDragId activeId, resolveDropTargetTableIndex overId with
                            | Some oldIndex, Some newIndex when oldIndex <> newIndex ->
                                updateTableOrder (oldIndex, newIndex)
                            | _ -> ()
                        | _ -> ()
                ),
                [| box isEditorModeTableTab; box updateTableOrder |]
            )


        let tableIds =
            React.useMemo (
                (fun () -> tables |> Seq.mapi (fun index _ -> mkTableDragId index) |> ResizeArray),
                [| box tables.TableCount; box tableNamesKey |]
            )

        let tableTabModels: TableTabViewModel[] =
            React.useMemo (
                (fun () -> [|
                    for index = 0 to tables.TableCount - 1 do
                        {
                            index = index
                            tableName = tables.[index].Name
                            isEditorMode = isEditorModeTableTab = Some index
                            isActive = activeView = ActiveView.Table index
                        }
                |]),
                [|
                    box tables.TableCount
                    box tableNamesKey
                    box isEditorModeTableTab
                    box activeView
                |]
            )

        let renameTable =
            React.useCallback (
                (fun (tableIndex: int) (newName: string) ->
                    match System.String.IsNullOrWhiteSpace newName || newName = tables.[tableIndex].Name with
                    | true -> ()
                    | false ->
                        arcFile.ArcTables().RenameTableAt(tableIndex, newName)
                        setArcFile (ArcFiles.refreshRef arcFile)
                        closeEditorMode ()
                ),
                [|
                    box arcFile
                    box setArcFile
                    box closeEditorMode
                    box tableNamesKey
                |]
            )

        React.Fragment [
            ArcFileFooterTabs.ContextMenu(tabsRef, setEditorMode, deleteTable)
            Html.div [
                prop.className "swt:bg-base-300"
                prop.children [

                    Html.div [
                        prop.ref tabsRef
                        prop.className
                            "swt:*:[--tab-border-color:var(--color-base-content)] swt:tabs swt:tabs-lift swt:w-full \
                                swt:overflow-x-auto swt:overflow-y-hidden swt:flex swt:flex-row swt:items-center \
                                swt:justify-start swt:pt-1 swt:*:border-b-0! swt:*:gap-1 swt:flex-nowrap swt:*:flex-nowrap"
                        prop.children [

                            Html.div [ // This element is a spacer to create some whitespace between the tabs and the left edge of the container.
                                prop.className "swt:tab swt:max-w-min swt:px-2!"
                                prop.style [ style.custom ("order", -99) ]
                            ]

                            ArcFileFooterTabs.MetadataTab(
                                metadataTabLabel,
                                activateMetadataView,
                                isActive = (activeView = ActiveView.Metadata)
                            )

                            if canRenderDataMap then
                                ArcFileFooterTabs.DataMapTab(
                                    activateDataMapView,
                                    isActive = (activeView = ActiveView.DataMap)
                                )

                            ArcFileFooterTabs.DragAndDropContainer(
                                tableIds,
                                handleDragEnd,
                                children =
                                    React.Fragment [

                                        for tableModel in tableTabModels do

                                            match tableModel.isEditorMode with
                                            | true ->
                                                ArcFileFooterTabs.EditTableNameTab(
                                                    tableModel.tableName,
                                                    renameTable tableModel.index,
                                                    closeEditorMode,
                                                    isActive = tableModel.isActive
                                                )
                                            | false ->
                                                ArcFileFooterTabs.TableTab(
                                                    tableModel.index,
                                                    tableModel.tableName,
                                                    (fun _ -> activateTableView tableModel.index),
                                                    isActive = tableModel.isActive,
                                                    onDoubleClick = (fun _ -> openTableNameEditor tableModel.index),
                                                    key = tableModel.index
                                                )

                                    ]
                            )


                            if canAddTable then
                                ArcFileFooterTabs.PlusBtn addNewTable

                            Html.div [ // This element is a spacer to create some whitespace between the tabs and the right edge of the container.
                                prop.className "swt:tab swt:max-w-min swt:px-2!"
                                prop.style [ style.custom ("order", System.Int32.MaxValue) ]
                            ]
                        ]
                    ]
                ]
            ]
        ]