namespace Swate.Components.Composite.Authentication

open Fable.Core
open Feliz
open Swate.Components
open Swate.Components.Primitive.BaseModal
open Swate.Components.Primitive.Dropdown
open Types
open Helper


[<Erase; Mangle(false)>]
type Authentication =

    [<ReactComponent>]
    static member private AvatarPlaceholder() =
        Html.div [
            prop.className [ "swt:avatar swt:avatar-placeholder swt:size-full" ]
            prop.children [
                Html.div [
                    prop.className "swt:bg-neutral swt:text-neutral-content swt:rounded-full swt:p-2"
                    prop.children [
                        Html.span [
                            prop.className "swt:size-full swt:iconify swt:fluent--person-12-filled"
                        ]
                    ]
                ]
            ]
        ]

    [<ReactComponent>]
    static member private UserAvatarIcon(userInformation: AuthUserDto) =
        Html.img [
            prop.className "swt:w-8 swt:h-8 swt:rounded-full"
            prop.src userInformation.AvatarUrl
            prop.alt userInformation.Name
        ]


    [<ReactComponent>]
    static member private CustomDataHubRadioItem(activeDataHub, radioid, setDataHub: DataHubInformation -> unit) =

        let customDataHub, setCustomDataHub = React.useState "https://"

        let mkCustomDataHubInfoFromUrl (url: string) : DataHubInformation = {
            Name = url
            Url = url
            Description = None
        }

        Html.label [
            prop.className "swt:label swt:gap-2"
            prop.children [
                Html.input [
                    prop.testId "CustomDataHubRadio"
                    prop.type'.radio
                    prop.name radioid
                    prop.className "swt:radio swt:radio-sm"
                    prop.isChecked (activeDataHub.Url = customDataHub)
                    prop.onChange (fun (_: bool) -> setDataHub (mkCustomDataHubInfoFromUrl customDataHub))
                ]
                Html.input [
                    prop.testId "CustomDataHubInput"
                    prop.className "swt:input swt:input-xs"
                    prop.value customDataHub
                    prop.placeholder "https://"
                    prop.onChange (fun value ->
                        setCustomDataHub value
                        setDataHub (mkCustomDataHubInfoFromUrl value)
                    )
                ]
            ]
        ]

    [<ReactComponent>]
    static member private DataHubRadioItem(activeDataHub, radioid, dataHub, setDataHub, ?radioClassName) =
        Html.label [
            prop.className "swt:label swt:gap-2"
            prop.children [
                Html.input [
                    prop.testId ($"DataHubRadio-{dataHub.Name}")
                    prop.type'.radio
                    prop.name radioid
                    prop.id dataHub.Url
                    prop.className (Option.defaultValue "swt:radio swt:radio-sm" radioClassName)
                    prop.isChecked (activeDataHub.Url = dataHub.Url)
                    prop.onChange (fun (_: bool) -> setDataHub dataHub)
                ]
                Html.span [ prop.text dataHub.Name; prop.title dataHub.Url ]
            ]
        ]

    [<ReactComponent>]
    static member private DataHubSelect(dataHub: DataHubInformation, setDataHub: DataHubInformation -> unit) =

        let radioid = React.useId ()

        Html.fieldSet [
            prop.className "swt:fieldset"
            prop.children [
                Html.legend [
                    prop.className "swt:fieldset-legend"
                    prop.text "Select DataHub"
                ]
                Html.div [
                    prop.className
                        "swt:flex swt:flex-col swt:w-full swt:cursor-not-allowed swt:gap-0.5 swt:p-2 swt:rounded swt:border swt:border-dashed swt:border-base-content"
                    prop.children [
                        Html.p [
                            prop.className "swt:text-base-content swt:text-lg swt:font-semibold"
                            prop.text dataHub.Name
                        ]
                        Html.p [
                            prop.className "swt:text-xs swt:text-base-content/70"
                            prop.text (dataHub.GetDescription())
                        ]
                        Html.a [
                            prop.href dataHub.Url
                            prop.target "_blank"
                            prop.rel "noopener noreferrer"
                            prop.className "swt:link swt:link-info"
                            prop.text dataHub.Url
                        ]
                    ]
                ]
                Html.div [
                    prop.className "swt:label"
                    prop.children [
                        Html.details [
                            prop.className "swt:collapse swt:w-full swt:collapse-arrow"
                            prop.children [
                                Html.summary [
                                    prop.testId "DataHubOptionsToggle"
                                    prop.className "swt:collapse-title swt:p-1"
                                    prop.children [ Html.span "Other options" ]
                                ]
                                Html.div [
                                    prop.className "swt:collapse-content swt:text-sm swt:flex swt:flex-col swt:gap-0.5"
                                    prop.children [
                                        Authentication.DataHubRadioItem(
                                            dataHub,
                                            radioid,
                                            Helper.Default_DataHub_Collection.Default,
                                            setDataHub,
                                            radioClassName = "swt:radio swt:radio-sm swt:radio-primary"
                                        )
                                        for dataHubI in Helper.Default_DataHub_Collection.Supported do
                                            Authentication.DataHubRadioItem(dataHub, radioid, dataHubI, setDataHub)
                                        Authentication.CustomDataHubRadioItem(dataHub, radioid, setDataHub)
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ]
        ]

    [<ReactComponent>]
    static member private NotAuthenticatedView
        (onSignIn: SignInInformation -> unit, setError: exn option -> unit, ?initialDataHubUrl)
        =

        let initialDataHubUrl = defaultArg initialDataHubUrl Helper.Default_DataHub
        let dataHubUrl, setDataHubUrl = React.useState initialDataHubUrl
        let pat, setPat = React.useState ""

        Html.div [
            prop.className "swt:flex swt:flex-col swt:p-4 swt:w-md swt:gap-2"
            prop.children [
                Html.h1 [
                    prop.text "DataHub Authentication"
                    prop.className "swt:text-center swt:text-3xl swt:font-bold swt:mb-2"
                ]
                Html.p [
                    prop.text "Sign in to your DataHub account to access your ARCs."
                    prop.className "swt:text-center swt:text-sm swt:text-base-content/80"
                ]
                Html.fieldSet [
                    prop.className "swt:fieldset"
                    prop.children [
                        Html.label [
                            prop.className "swt:fieldset-legend"
                            prop.text "Personal Access Token"
                        ]
                        Html.input [
                            prop.testId "PersonalAccessTokenInput"
                            prop.className "swt:input swt:w-full"
                            prop.placeholder "Personal Access token"
                            prop.type'.password
                            prop.value pat
                            prop.onChange (fun value -> setPat value)
                        ]
                    ]
                ]
                Authentication.DataHubSelect(dataHubUrl, setDataHubUrl)
                Html.a [
                    prop.testId "GeneratePatLink"
                    prop.className "swt:link swt:link-info swt:text-sm swt:text-center swt:py-2"
                    prop.text "Click here to generate a new GitLab Personal Access Token"
                    prop.href (Helper.GitLabUrls.prefillGitLabPATScopes dataHubUrl.Url)
                    prop.target.blank
                    prop.rel "noopener noreferrer"
                ]
                Html.button [
                    prop.testId "SignInButton"
                    prop.className "swt:btn swt:btn-primary swt:btn-lg swt:w-full"
                    prop.text "Sign In"
                    prop.disabled (
                        System.String.IsNullOrWhiteSpace pat
                        || System.String.IsNullOrWhiteSpace dataHubUrl.Url
                    )
                    prop.onClick (fun _ ->
                        onSignIn {
                            GitLabBaseUrl = dataHubUrl.Url
                            PersonalAccessToken = pat
                            OnErrorCallback = fun ex -> setError (Some ex)
                        }
                    )
                ]
                Html.p [
                    prop.className "swt:text-sm swt:text-center swt:text-base-content/80"
                    prop.text "Don't have an account? Sign up on your DataHub instance to create one."
                ]
            ]
        ]

    [<ReactComponent>]
    static member LogoutBtn(onLogout: unit -> unit) =
        Html.button [
            prop.testId "LogoutButton"
            prop.className "swt:btn swt:btn-error swt:btn-outline swt:btn-sm swt:w-full"
            prop.text "Logout"
            prop.onClick (fun _ -> onLogout ())
        ]

    [<ReactComponent>]
    static member AddAnotherAccountBtn(onAddAccount: unit -> unit) =
        Html.button [
            prop.testId "AddAnotherAccountButton"
            prop.className "swt:btn swt:btn-sm swt:btn-ghost swt:w-full swt:gap-1"
            prop.onClick (fun _ -> onAddAccount ())
            prop.text "Add another account"
        ]

    [<ReactComponent>]
    static member UserAvatar
        (
            accounts: AuthStateDto,
            onSignIn: SignInInformation -> unit,
            onLogout: unit -> unit,
            ?isLoading: bool,
            ?dropdownClassName: string,
            ?onRotateToken: string -> unit,
            ?onSwitchAccount: string -> unit,
            ?onRemoveAccount: string -> unit
        ) =

        let isOpen, setIsOpen = React.useState false
        let error, setError = React.useState (None: exn option)
        let showAddAccount, setShowAddAccount = React.useState false

        let initialDataHubUrl, setInitialDataHubUrl =
            React.useState (None: DataHubInformation option)

        let activeUser = accounts.ActiveUser()

        let onSignIn =
            fun signInInfo ->
                setError None
                setIsOpen false
                setInitialDataHubUrl None
                setShowAddAccount false
                onSignIn signInInfo

        let ToggleContent =
            React.useMemo (
                (fun () ->
                    match isLoading, activeUser with
                    | Some true, _ ->
                        Html.span [
                            prop.className "swt:loading swt:loading-spinner swt:loading-sm"
                        ]
                    | _, Some userInformation -> Authentication.UserAvatarIcon(userInformation)
                    | _, None -> Authentication.AvatarPlaceholder()
                ),
                [| box activeUser; box isLoading; box error |]
            )

        let onRegenerateToken (account: AccountSummary) =
            setShowAddAccount true

            (Some >> setInitialDataHubUrl) {
                Name = account.User.TargetDataHub
                Url = account.User.TargetDataHub
                Description =
                    Some
                        "A browser window with prefilled scopes for regenerating your token will open. Please generate a new token and use it to sign in again."
            }

        let content =
            React.useMemo (
                (fun () ->
                    match activeUser with
                    | Some user ->
                        if showAddAccount then
                            Html.div [
                                prop.className "swt:flex swt:flex-col swt:gap-2"
                                prop.children [
                                    Html.button [
                                        prop.testId "AddAccountBackButton"
                                        prop.className "swt:btn swt:btn-sm swt:btn-ghost swt:self-start"
                                        prop.onClick (fun _ -> setShowAddAccount false)
                                        prop.text "\u2190 Back to account"
                                    ]
                                    Authentication.NotAuthenticatedView(
                                        onSignIn,
                                        setError,
                                        ?initialDataHubUrl = initialDataHubUrl
                                    )
                                ]
                            ]
                        else
                            Html.div [
                                prop.className "swt:flex swt:flex-col swt:gap-2"
                                prop.children [
                                    AccountManager.Main(
                                        accounts,
                                        onRegenerateToken = onRegenerateToken,
                                        ?onRotateToken = onRotateToken,
                                        ?onSwitchAccount = onSwitchAccount,
                                        ?onRemoveAccount = onRemoveAccount
                                    )
                                    Authentication.LogoutBtn onLogout
                                    Authentication.AddAnotherAccountBtn(fun () -> setShowAddAccount true)
                                ]
                            ]
                    | None -> Authentication.NotAuthenticatedView(onSignIn, setError)
                ),
                [|
                    box activeUser
                    box error
                    box showAddAccount
                    box accounts
                    box initialDataHubUrl
                |]
            )

        React.Fragment [
            BaseModal.ErrorModalObsolete(
                error.IsSome,
                (fun _ -> setError None),
                error
                |> Option.map _.Message
                |> Option.defaultValue "An unknown error occurred."
            )
            Dropdown.Main(
                isOpen,
                setIsOpen,
                Html.div [
                    prop.className "swt:indicator swt:indicator-bottom"
                    prop.children [
                        match activeUser, accounts.ActiveAccount with
                        | Some _, Some activeAccount ->
                            match activeAccount.TokenStatus with
                            | TokenStatus.Invalid ->
                                Html.span [
                                    prop.testId "TokenInvalidIndicator"
                                    prop.className "swt:indicator-item"
                                    prop.ariaLabel
                                        "Your token is invalid. Please update your token or remove the account."
                                    prop.title "Your token is invalid. Please update your token or remove the account."
                                    prop.children [
                                        Html.i [
                                            prop.ariaHidden true
                                            prop.className "swt:iconify swt:fluent--warning-12-filled swt:text-error"
                                        ]
                                    ]
                                ]
                            | TokenStatus.Expiring ->
                                Html.span [
                                    prop.testId "TokenExpiringIndicator"
                                    prop.className "swt:indicator-item"
                                    prop.ariaLabel "Your token is expiring soon. Rotate it to avoid interruption."
                                    prop.title (
                                        activeAccount.TokenExpiresOn
                                        |> Option.map (fun expiresOn ->
                                            $"Your token expires on {expiresOn}. Rotate it soon to avoid interruption."
                                        )
                                        |> Option.defaultValue
                                            "Your token is expiring soon. Rotate it to avoid interruption."
                                    )
                                    prop.children [
                                        Html.i [
                                            prop.ariaHidden true
                                            prop.className "swt:iconify swt:fluent--warning-12-filled swt:text-warning"
                                        ]
                                    ]
                                ]
                            | TokenStatus.Ok -> ()
                        | _ -> ()
                        Html.button [
                            prop.testId "UserButtonToggle"
                            prop.onClick (fun _ -> setIsOpen (not isOpen))
                            prop.className "swt:btn swt:btn-circle swt:btn-sm"
                            prop.children [ ToggleContent ]
                        ]
                    ]
                ],
                Html.div [ prop.testId "UserDropdownContent"; prop.children content ],
                contentClassName =
                    "swt:flex swt:flex-col swt:min-w-xs swt:gap-2 swt:border-base-content swt:border-1 swt:p-2 swt:shadow-lg swt:rounded swt:bg-base-100 swt:top-[110%]",
                closeOnClick = false,
                ?dropdownClassName = dropdownClassName
            )
        ]

    static member ExmpUserInformation = {
        Id = 1
        LocalSwateAccountId = "acc-1"
        Name = "John Doe"
        Email = "john-doe@mail.com"
        AvatarUrl = "https://www.gravatar.com/avatar/00000000000000000000000000000000?d=mp&f=y"
        TargetDataHub = Helper.Default_DataHub_Url
    }

    static member User = {
        Types.AuthStateDto.Empty with
            ActiveAccount =
                Some {
                    User = Authentication.ExmpUserInformation
                    DateAdded = "2026-01-01T00:00:00.0000000Z"
                    TokenStatus = TokenStatus.Ok
                    TokenExpiresOn = Some "2026-02-01"
                }
            StoredAccounts = [|
                {
                    User = Authentication.ExmpUserInformation
                    DateAdded = "2026-01-01T00:00:00.0000000Z"
                    TokenStatus = TokenStatus.Ok
                    TokenExpiresOn = Some "2026-02-01"
                }
            |]
    }

    [<ReactComponent(true)>]
    static member Entry(?user) =
        let accounts, setAccounts = React.useState (defaultArg user AuthStateDto.Empty)

        let isLoading, setIsLoading = React.useState false

        let onSignIn =
            fun (signInInfo: SignInInformation) ->
                promise {
                    setIsLoading true
                    do! Promise.sleep 1000
                    // Testing, ignore for story tests
                    // let! userInformation = GitLabAPI.getUserAPIRequest signInInfo
                    // Here should be try get user information from the DataHub using the provided signInInfo and handle possible errors by calling signInInfo.OnErrorCallback with the error message. For now we just simulate a successful sign in with example user information and a delay.
                    let activeUser = {
                        Authentication.ExmpUserInformation with
                            TargetDataHub = signInInfo.GitLabBaseUrl
                    }

                    setAccounts {
                        ActiveAccount =
                            Some {
                                User = {
                                    Id = activeUser.Id
                                    LocalSwateAccountId = activeUser.LocalSwateAccountId
                                    Name = activeUser.Name
                                    Email = activeUser.Email
                                    AvatarUrl = activeUser.AvatarUrl
                                    TargetDataHub = activeUser.TargetDataHub
                                }
                                DateAdded = "2026-01-01T00:00:00.0000000Z"
                                TokenStatus = TokenStatus.Ok
                                TokenExpiresOn = Some "2026-02-01"
                            }
                        StoredAccounts = [|
                            {
                                User = {
                                    Id = activeUser.Id
                                    LocalSwateAccountId = activeUser.LocalSwateAccountId
                                    Name = activeUser.Name
                                    Email = activeUser.Email
                                    AvatarUrl = activeUser.AvatarUrl
                                    TargetDataHub = activeUser.TargetDataHub
                                }
                                DateAdded = "2026-01-01T00:00:00.0000000Z"
                                TokenStatus = TokenStatus.Ok
                                TokenExpiresOn = Some "2026-02-01"
                            }
                            {
                                User = {
                                    Id = 2
                                    LocalSwateAccountId = "acc-2"
                                    Name = "Max Mustermann"
                                    Email = "max@example.org"
                                    AvatarUrl =
                                        "https://www.gravatar.com/avatar/22222222222222222222222222222222?d=mp&f=y"
                                    TargetDataHub = "https://datahub.rz.rptu.de/"
                                }
                                DateAdded = "2026-01-02T00:00:00.0000000Z"
                                TokenStatus = TokenStatus.Expiring
                                TokenExpiresOn = Some "2026-01-15"
                            }
                            {
                                User = {
                                    Id = 3
                                    LocalSwateAccountId = "acc-3"
                                    Name = "Mr Lazy"
                                    Email = "lazy@example.org"
                                    AvatarUrl =
                                        "https://www.gravatar.com/avatar/33333333333333333333333333333333?d=mp&f=y"
                                    TargetDataHub = "https://git.nfdi4plants.org/"
                                }
                                DateAdded = "2022-01-02T00:00:00.0000000Z"
                                TokenStatus = TokenStatus.Invalid
                                TokenExpiresOn = Some "2022-02-01"
                            }
                        |]
                    }

                    setIsLoading false
                }
                |> Promise.start

        let onLogout () = setAccounts AuthStateDto.Empty

        let onSwitchAccount (localSwateAccountId: string) =
            let nextActive =
                accounts.StoredAccounts
                |> Array.tryFind (fun account -> account.User.LocalSwateAccountId = localSwateAccountId)
                |> Option.orElse accounts.ActiveAccount
                |> Option.orElse (accounts.StoredAccounts |> Array.tryHead)

            let next = {
                ActiveAccount = nextActive
                StoredAccounts = accounts.StoredAccounts
            }

            setAccounts next

        let onRemoveAccount (localSwateAccountId: string) =
            let filteredAccounts =
                accounts.StoredAccounts
                |> Array.filter (fun account -> account.User.LocalSwateAccountId <> localSwateAccountId)

            let nextActive =
                match accounts.ActiveAccount with
                | Some activeAccount when activeAccount.User.LocalSwateAccountId <> localSwateAccountId ->
                    filteredAccounts
                    |> Array.tryFind (fun account ->
                        account.User.LocalSwateAccountId = activeAccount.User.LocalSwateAccountId
                    )
                | _ -> filteredAccounts |> Array.tryHead

            let next = {
                ActiveAccount = nextActive
                StoredAccounts = filteredAccounts
            }

            setAccounts next

        let onRotateToken (localSwateAccountId: string) =
            // For testing, we just set the token status to Ok and update the expiration date. In a real implementation, this should trigger the token rotation flow and update the account information accordingly.
            let nextAccounts =
                accounts.StoredAccounts
                |> Array.map (fun account ->
                    if account.User.LocalSwateAccountId = localSwateAccountId then
                        {
                            account with
                                TokenStatus = TokenStatus.Ok
                                TokenExpiresOn = Some "2026-03-01"
                        }
                    else
                        account
                )

            let nextActive =
                match accounts.ActiveAccount with
                | Some activeAccount when activeAccount.User.LocalSwateAccountId = localSwateAccountId ->
                    nextAccounts
                    |> Array.tryFind (fun account ->
                        account.User.LocalSwateAccountId = activeAccount.User.LocalSwateAccountId
                    )
                | other -> other

            setAccounts {
                ActiveAccount = nextActive
                StoredAccounts = nextAccounts
            }

        Html.div [
            prop.className "swt:flex swt:flex-col swt:m-10 swt:gap-2"
            prop.children [
                Html.h1 [
                    prop.text "Authentication"
                    prop.className "swt:text-2xl swt:font-bold swt:mb-4"
                ]
                Html.p [
                    prop.testId "SignedInInfo"
                    prop.textf "Signed In: %b" (accounts.ActiveUser() |> Option.isSome)
                ]
                Authentication.UserAvatar(
                    accounts,
                    onSignIn,
                    onLogout,
                    isLoading = isLoading,
                    onSwitchAccount = onSwitchAccount,
                    onRemoveAccount = onRemoveAccount,
                    onRotateToken = onRotateToken
                )
            ]
        ]
